OUTPUT_ARCH(i386)
ENTRY(__entry)
SECTIONS {
	.text 0xc0100000 : AT(0x100000) {
		text = .;
		*(.text)
		*(.text*)
		*(.rodata)
		*(.rodata*)
		/*
		 * Init/exit definitions after the readonly data; there's no
		 * reason why these should be writable or take up a section of
		 * their own.
		 */
		__initfuncs_begin = ALIGN(4);
		*(initfuncs)
		__initfuncs_end = .;
		__exitfuncs_begin = ALIGN(4);
		*(exitfuncs)
		__exitfuncs_end = .;
		. = ALIGN(4096);
	}
	.data : {
		*(.data)
		*(.data*)
		. = ALIGN(4096);
	}
	.bss : {
		*(.bss)
		*(.bss*)
		*(COMMON)
		. = ALIGN(4096);
	}
	__end = . ;
	__traceid_begin = .;
	.traceids : {
		*(.traceids)
	}
	__traceid_end = .;
	.tracenames : {
		*(.tracenames)
	}
}
